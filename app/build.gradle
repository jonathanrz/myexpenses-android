apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'testfairy'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"
    def MYEXPENSES_AUTH_TOKEN = '"' + System.getenv("MYEXPENSES_AUTH_TOKEN") + '"' ?: '"Auth Token"';

    dexOptions {
        maxProcessCount 2
        javaMaxHeapSize "2g"
    }

    signingConfigs {
        jonathan {
            storeFile file("jonathan.keystore")
            storePassword System.getenv("MYEXPENSES_JONATHAN_KEY_PASS")
            keyAlias "jonathanrz"
            keyPassword System.getenv("MYEXPENSES_JONATHAN_KEY_PASS")
        }
        thainara {
            storeFile file("thainara.keystore")
            storePassword System.getenv("MYEXPENSES_THAINARA_KEY_PASS")
            keyAlias "jonathanrz"
            keyPassword System.getenv("MYEXPENSES_THAINARA_KEY_PASS")
        }
    }

    defaultConfig {
        applicationId "br.com.jonathanzanella.myexpenses"
        minSdkVersion 19
        targetSdkVersion 23
        versionCode 10
        versionName "0.4.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            buildConfigField 'String', 'MYEXPENSES_AUTH_TOKEN', MYEXPENSES_AUTH_TOKEN
        }
        jonathan {
            applicationIdSuffix '.jonathan'
            versionNameSuffix '-JONATHAN'
            buildConfigField 'String', 'MYEXPENSES_AUTH_TOKEN', MYEXPENSES_AUTH_TOKEN
            debuggable true

            // NB: If you want to use the default debug key for a (non-debug)
            // build type, you need to specify it:
            signingConfig signingConfigs.jonathan
        }
        thainara {
            applicationIdSuffix '.thainara'
            versionNameSuffix '-THAINARA'
            buildConfigField 'String', 'MYEXPENSES_AUTH_TOKEN', MYEXPENSES_AUTH_TOKEN
            debuggable true

            // NB: If you want to use the default debug key for a (non-debug)
            // build type, you need to specify it:
            signingConfig signingConfigs.thainara
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField 'String', 'MYEXPENSES_AUTH_TOKEN', MYEXPENSES_AUTH_TOKEN
        }
    }
    testfairyConfig {
        apiKey System.getenv("MYEXPENSES_TEST_FAIRY_TOKEN")
        autoUpdate false
        notify true
        testersGroups "dev"
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    //Support Library
    def supportLibrary = '23.1.1'
    compile "com.android.support:appcompat-v7:${supportLibrary}"
    compile "com.android.support:design:${supportLibrary}"
    compile "com.android.support:cardview-v7:${supportLibrary}"
    compile "com.android.support:support-annotations:${supportLibrary}"
    androidTestCompile "com.android.support:support-annotations:${supportLibrary}"

    def playServices = '9.0.0'
    compile "com.google.android.gms:play-services-gcm:${playServices}"

    //DBFlow
    def dbFlow = '3.0.1'
    apt "com.github.Raizlabs.DBFlow:dbflow-processor:${dbFlow}"
    compile "com.github.Raizlabs.DBFlow:dbflow-core:${dbFlow}"
    compile "com.github.Raizlabs.DBFlow:dbflow:${dbFlow}"

    //Lombok
    def lombok = '1.12.6'
    provided "org.projectlombok:lombok:${lombok}"
    apt "org.projectlombok:lombok:${lombok}"

    //retrofit
    def retrofit = '2.0.2'
    compile "com.squareup.retrofit2:retrofit:${retrofit}"
    compile "com.squareup.retrofit2:converter-gson:${retrofit}"
    compile "com.squareup.retrofit2:adapter-rxjava:${retrofit}"
    //RxAndroid
    compile 'io.reactivex:rxandroid:1.0.1'
    compile 'io.reactivex:rxjava:1.0.14'

    compile "com.jakewharton:butterknife:7.0.1"
    compile 'net.danlew:android.joda:2.9.1'
    compile 'com.facebook.stetho:stetho:1.3.0'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'com.timehop.stickyheadersrecyclerview:library:0.4.3@aar'

    //tests
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile 'com.android.support.test:runner:0.4'
    androidTestCompile 'com.android.support.test:rules:0.4'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.1'
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
}