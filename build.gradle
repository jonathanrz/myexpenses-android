// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.1.4-3'
    repositories {
        jcenter()
        maven { url 'https://maven.google.com' }
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.0-beta4'
        classpath 'com.dicedmelon.gradle:jacoco-android:0.1.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        //noinspection GradleDynamicVersion
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

allprojects {
    repositories {
        jcenter()
        maven { url "https://jitpack.io" }
        maven { url 'https://maven.google.com' }
        maven { url 'https://maven.fabric.io/public' }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    // Sdk and tools
    minSdkVersion = 21
    targetSdkVersion = 26
    compileSdkVersion = 26
    buildToolsVersion = '26.0.0'

    // App dependencies
    ankoVersion = '0.9.1'
    commonsLangVersion = '3.5'
    crashlyticsVersion = '2.6.8'
    daggerVersion = '2.11'
    espressoVersion = '2.2.2'
    hamcrestVersion = '1.3'
    jodaVersion = '2.9.9'
    junitVersion = '4.12'
    mockitoKotlinVersion = '1.5.0'
    playServicesVersion = '11.2.0'
    retrofitVersion = '2.2.0'
    roomVersion = '1.0.0-alpha5'
    rxAndroidVersion = '2.0.1'
    rxJavaVersion = '2.1.0'
    rulesVersion = '0.5'
    runnerVersion = '0.5'
    stethoVersion = '1.4.2'
    stickyHeadersVersion = '0.4.3@aar'
    supportLibraryVersion = '26.0.2'
    timberVersion = '4.5.1'
    uiAutomatorVersion = '2.1.1'
}

ext.getCurrentBranch = {
    def branch = System.getenv('TRAVIS_PULL_REQUEST_BRANCH')
    if (branch?.trim()) {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--abbrev-ref', 'HEAD'
            standardOutput = stdout
        }
        branch = stdout.toString().trim()
    }
    return branch
}

ext.gitCommitCount = {
    if(project.hasProperty('devBuild'))
        return 1

    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-list', '--count', 'HEAD'
        standardOutput = stdout
    }
    def commitCount = stdout.toString().trim().toInteger()
    return commitCount
}

ext.getVersionName = {
    if(project.hasProperty('devBuild'))
        return "dev"

    def branch = getCurrentBranch()

    def hasBranch = branch?.trim()

    if (!hasBranch) {
        return gitCommitCount()
    } else {
        return branch
    }
}
